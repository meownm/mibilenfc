buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:8.1.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.10"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

subprojects {
    configurations.all {
        resolutionStrategy {
            force "org.bouncycastle:bcprov-jdk15to18:1.70"
            force "org.bouncycastle:bcpkix-jdk15to18:1.70"

            eachDependency { details ->
                // jmrtd depends on BouncyCastle's "jdk18on" artifacts, which are not
                // compatible with Android. We substitute them with the "jdk15to18"
                // variants.
                if (details.requested.group == "org.bouncycastle" && details.requested.name.contains("jdk18on")) {
                    details.useTarget(
                        group: details.requested.group,
                        name: details.requested.name.replace("jdk18on", "jdk15to18"),
                        version: "1.70"
                    )
                    details.because("Android requires the 'jdk15to18' Bouncy Castle artifacts.")
                }
            }
        }
    }
}
